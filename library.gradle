apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion project.properties.sdk_version
    defaultConfig {
        minSdkVersion project.properties.min_sdk_version
        targetSdkVersion project.properties.sdk_version
    }
    testOptions {
        unitTests.all {
            testLogging {
                outputs.upToDateWhen { false }
                events "started", "skipped", "failed", "passed", "standardError"
                exceptionFormat "full" //short
                showCauses true
                showExceptions true
                showStackTraces true
                showStandardStreams true
            }
        }
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.robolectric:robolectric:4.8'
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.renetik.library'
                artifactId project.name
                artifact sourceJar
                version = project.renetik_android_version
            }
        }
    }
}
task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}
dokkaHtml {
    outputDirectory = file("${projectDir}/../docs")
    dokkaSourceSets {
        configureEach {
            includes.from("../README.md")
        }
    }
}